/*
* This file estimated a NK Model for a Closed Economy.
*
*/

var c rnom pic lab rw y mc a g z rnom_obs pic_obs y_obs;
varexo e_a e_g e_z;

parameters beta_C sigma sigma_L theta_p phi_pic phi_y infl_ss crec_ss rho_a rho_g rho_z rhor;

beta_C  = 0.995;
sigma   = 1;
sigma_L = 1;
theta_p = 0.75;
phi_pic = 1.5;
phi_y   = 0.125;
rhor = 0.3;
infl_ss = 2;
crec_ss = 2;

rho_a   = 0.90;
rho_g   = 0.80;
rho_z   = 0*0.70;


model (linear);
c   = -sigma*(rnom-pic(+1)) + c(+1)  + g*(1-rho_g)*sigma;
rw  = sigma_L*lab + (1/sigma)*c;
y   = a + lab;
pic = beta_C*pic(+1) + (1-theta_p)*(1-theta_p*beta_C)/theta_p*mc;
mc = rw - a;
rnom= rhor*rnom(-1)+ (1-rhor)*(phi_pic*pic + phi_y*y) + z;
y   = c;
a   = rho_a*a(-1) + e_a;
g   = rho_g*g(-1) + e_g;
z   = rho_z*z(-1) + e_z;

rnom_obs=infl_ss+crec_ss-400*log(beta_C)+400*rnom;
pic_obs=infl_ss+400*pic;
y_obs=crec_ss+400*(y-y(-1));

end;

initval;
c =0;
rw  =0;
y   = 0;
pic = 0;
mc = 0;
rnom= 0;
y   = 0;
a   = 0;
g   = 0;
z   = 0;
pic_obs=infl_ss;
rnom_obs=infl_ss;
y_obs=crec_ss;

end;


steady;
check;

estimated_params;
beta_C,0.995,,,GAMMA_PDF,0.995,0.001;
sigma,0.5031,,,GAMMA_PDF,1,0.5;
sigma_L,1.1702,,,GAMMA_PDF,1,0.5;
theta_p,0.6626,,,BETA_PDF,0.75,0.05;
phi_pic,2.5765,,,GAMMA_PDF,1.5,.25;
phi_y,0.0290,,,GAMMA_PDF,.125,.05;
crec_ss,2,,,NORMAL_PDF,3,1;
infl_ss,2,,,NORMAL_PDF,3,1;
rho_a,0.3,,,BETA_PDF,0.3,0.1;
rho_g,0.3,,,BETA_PDF,0.3,0.1;
%rho_z,0.3,,,BETA_PDF,0.3,0.1;
stderr e_a,0.01,,,GAMMA_PDF,0.01,0.005;
stderr e_g,0.01,,,GAMMA_PDF,0.01,0.005;
stderr e_z,0.01,,,GAMMA_PDF,0.01,0.005;
rhor,0.8,,,BETA_PDF,0.8,0.1;

end;

varobs rnom_obs pic_obs y_obs;

% estimation(datafile=TransformedData,
% xls_range = C1:E117,
% xls_sheet = Data,
% mh_replic=1000,
% mh_nblocks=1,
% mh_jscale=0.53,
% mode_compute=0, %4,
% prior_trunc=1e-32,
% mode_file=nk_closed_est_mode,
% nobs = 104, % Uses data until 2009q4 for estimation
% forecast=8
% ) rnom_obs pic_obs y_obs ;

% Forecast evaluation dynare
estimation(datafile=TransformedData,
xls_range = C1:E117,
xls_sheet = Data,
mh_replic=1000,
mh_nblocks=1,
mh_jscale=0.53,
mode_compute=0,
prior_trunc=1e-32,
mode_file=nk_closed_est_mode,
nobs = [104:112], %112], % nobs=104 means that uses data until 2009q4 for estimation
forecast=8,
nodiagnostic,
nograph,
graph_format = none
) rnom_obs pic_obs y_obs ;
